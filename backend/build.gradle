plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco' // Plugin de JaCoCo para cobertura de código
    id "org.sonarqube" version "7.1.0.6387"
}

group = 'pe.edu.upc.oss.group1'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
}

jacoco {
    toolVersion = "0.8.12" // Versión de JaCoCo
}

tasks.named('test') {
	useJUnitPlatform()
    finalizedBy jacocoTestReport // Asegura que el reporte de JaCoCo se genere después de los tests
}

jacocoTestReport {
    dependsOn test // Asegura que los tests se ejecuten antes de generar el reporte
    reports {
        xml.required = true // Genera reporte XML para SonarQube
        csv.required = false
        html.required = false
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "DevPicon_oss-project-upc"
        property "sonar.organization", "devpicon"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml" // Ruta del reporte de cobertura
        // property "sonar.login", System.getenv("SONAR_TOKEN") // Esto se configurará en GitHub Actions como secreto
    }
}
